%let pgm=utl-save-sas-tabulate-freq-report-listings-in-sas-tables;

%stop_submission;

Save crosstab listings for sas tabulate freq report in sas tables

CONTENTS

    1 proc tabulate to sas datasets
    2 proc report   to sas datasets
    3 proc freq     to sas datasets

github
https://tinyurl.com/yf64x4ws
https://github.com/rogerjdeangelis/utl-save-sas-tabulate-freq-report-listings-in-sas-tables

Should work for other procedures like means, print ...

Original work by
Bartosz Jablonski
yabwon@gmail.com

SOAPBOX ON

   The orginal reason for ods was to produce datasets from the displayed
   printed output. However many procs failed tp produce output
   that matches the display. Notable are
   proc tabulate and  Proc freq. This post tries to fill the gap by
   structuring the ods listing output into a delimited file and
   importing the file into a sas dataset. One notable exception is "ods excel'
   which does produce sheets that look like proc output.
   You can output to excel and convert the sheet to a sas dataset.

SOAPBOX ON

Perhaps tsas templates could enhace this algorithm/

I had an epiphany about macros for this problem.
Too difficult to create general macros.
It is quite easy to add a hort cleanup step.

github  (macros are questionable.
https://tinyurl.com/mrx5btmz
https://github.com/rogerjdeangelis/utl-save-crosstab-proc-tabulate-feq-report-printed-output-in-sas-tables

For the follwing create sas table instaed of static lineprinter listings

/**************************************************************************************************************************/
/*         INPUT          |          PROCESS                                   |                                          */
/*                        |                                                    |                                          */
/*   DATA SORTED FOR      |  SUMBY AY AND AM THEN USE                          |                                          */
/*   DOCUMENTATION ONLY   |  PER FOR COLUMN NAME USING TABULATE REPORT FREQ    |                                          */
/*   SOLUTIONS OK NOTSORTD|                                                    |                                          */
/*                        |                                                    | WORK.WANT                                */
/*         INPUT          |           SUM      SUM      SUM      SUM           |  AY   AM    AMT1  AMT2  AMT3  AMT4       */
/*                        |           AMT6     AMT7     AMT8     AMT9          |                                          */
/*    AY  AM PER AMT      |           per=6    per=7    per=8    per=9         | 2018   2      22   24     .     .        */
/*                        |                                                    | 2018   3      20   24    24     .        */
/*   2018  2  6   11      |  2018 2  11+11     12+12    .+.       .+.          | 2018   4       .    .     .    84        */
/*   2018  2  6   11      |  2018 3  10+10     12+12    12+12     .+.          | 2019   3      60   24     .     .        */
/*   2018  2  7   12      |  ...                                               | 2019   4     100   22    22    22        */
/*   2018  2  7   12      |  -------------------------------------             |                                          */
/*                        | |        |            PER            |             |                                          */
/*   2018  3  6   10      | |        |---------------------------|             |                                          */
/*   2018  3  6   10      | |        |  6   |  7   |  8   |  9   |             |                                          */
/*   2018  3  7   12      | |        |------+------+------+------|             |                                          */
/*   2018  3  7   12      | |        | AMT  | AMT  | AMT  | AMT  |             |                                          */
/*   2018  3  8   12      | |        |------+------+------+------|             |                                          */
/*   2018  3  8   12      | |        | Sum  | Sum  | Sum  | Sum  |             |                                          */
/*                        | |--------+------+------+------+------|             |                                          */
/*   2018  4  9   42      | |AY  |AM |      |      |      |      |             |                                          */
/*   2018  4  9   42      | |----+---|      |      |      |      |             |                                          */
/*                        | |2018|2  |    22|    24|      |      |             |                                          */
/*   2019  3  6   30      | |    |---+------+------+------+------|             |                                          */
/*   2019  3  6   30      | |    |3  |    20|    24|    24|      |             |                                          */
/*   2019  3  7   12      | |    |---+------+------+------+------|             |                                          */
/*   2019  3  7   12      | |    |4  |      |      |      |    84|             |                                          */
/*                        | |----+---+------+------+------+------|             |                                          */
/*   2019  4  6   50      | |2019|3  |    60|    24|      |      |             |                                          */
/*   2019  4  6   50      | |    |---+------+------+------+------|             |                                          */
/*   2019  4  7   11      | |    |4  |   100|    22|    22|      |             |                                          */
/*   2019  4  7   11      | --------------------------------------             |                                          */
/*   2019  4  8   11      |                                                    |                                          */
/*   2019  4  8   11      |  KEY POINTS                                        |                                          */
/*   2019  4  9   11      |     1 use options  FORMCHAR='|' ;                  |                                          */
/*   2019  4  9   11      |     2 use title1 "|AY|AM|AMT6|AMT7|AMT8|AMT9"      |                                          */
/*                        |       for column names                             |                                          */
/*                        |     3 use ods listing="d:/txt/tab.txt"             |                                          */
/*                        |       this produces                                |                                          */
/*                        |                                                    |                                          */
/*                        |       |AY|AM|AMT6|AMT7|AMT8|AMT9                   |                                          */
/*                        |                                                    |                                          */
/*                        |       |       |6   |7   |8   |9   |                |                                          */
/*                        |                                                    |                                          */
/*                        |       |2018 |2|  22|  24|    |    |                |                                          */
/*                        |       |                                            |                                          */
/*                        |       |     |3|  20|  24|  24|    |                |                                          */
/*                        |       |                                            |                                          */
/*                        |       |     |4|    |    |    |  84|                |                                          */
/*                        |                                                    |                                          */
/*                        |       |2019 |3|  60|  24|    |    |                |                                          */
/*                        |       |                                            |                                          */
/*                        |       |     |4| 100|  22|  22|  22|                |                                          */
/*                        |                                                    |                                          */
/*                        |    4 Use proc import with options                  |                                          */
/*                        |         delim="|'                                  |                                          */
/*                        |         datarow=5                                  |                                          */
/*                        |                                                    |                                          */
/*                        |    5 do final cleanup                              |                                          */
/*                        |        data want;                                  |                                          */
/*                        |          set prewant(                              |                                          */
/*                        |             drop=var:                              |                                          */
/*                        |             where=(not missing(am)));              |                                          */
/*                        |        run;quit;                                   |                                          */
/*                        |                                                    |                                          */
/*                        |    6 There is an issue with some of these,         |                                          */
/*                        |      numeric data is misrepresented as number      |                                          */
/*                        |                                                    |                                          */
/**************************************************************************************************************************/

/*               _
 _ __  _ __ ___ | |__   ___ _ __ ___
| `_ \| `__/ _ \| `_ \ / _ \ `_ ` _ \
| |_) | | | (_) | |_) |  __/ | | | | |
| .__/|_|  \___/|_.__/ \___|_| |_| |_|
|_|
*/

CREATE TABLES FOR THESE THREE LINE PRINTER OUTPUTs
===================================================

options
validvarname=upcase;
libname sd1 "d:/sd1";
data sd1.have;
input ay am per amt;
 ay_am=catx('_',ay,am);
cards4;
2018 2 6 11
2018 2 6 11
2018 2 7 12
2018 2 7 12
2018 3 8 12
2018 3 8 12
2018 4 9 42
2018 4 9 42
2018 3 6 10
2018 3 6 10
2018 3 7 12
2018 3 7 12
2019 3 6 30
2019 3 6 30
2019 3 7 12
2019 3 7 12
2019 4 6 50
2019 4 6 50
2019 4 7 11
2019 4 7 11
2019 4 8 11
2019 4 8 11
2019 4 9 11
2019 4 9 11
;;;;
run;quit;

/*                          _        _           _       _
/ |  _ __  _ __ ___   ___  | |_ __ _| |__  _   _| | __ _| |_ ___
| | | `_ \| `__/ _ \ / __| | __/ _` | `_ \| | | | |/ _` | __/ _ \
| | | |_) | | | (_) | (__  | || (_| | |_) | |_| | | (_| | ||  __/
|_| | .__/|_|  \___/ \___|  \__\__,_|_.__/ \__,_|_|\__,_|\__\___|
    |_|
*/

/*---- Craete a sas dataset instead of just line printer type output for these proceedures ----*/

proc tabulate data=sd1.have;
class ay am per;
table ay*am, per*amt*(sum) /rts=13;
var amt;
run;quit;


 ----------------------------------------------------------------
|           |                        PER                        |
|           |---------------------------------------------------|
|           |     6      |     7      |     8      |     9      |
|           |------------+------------+------------+------------|
|           |    AMT     |    AMT     |    AMT     |    AMT     |
|           |------------+------------+------------+------------|
|           |    Sum     |    Sum     |    Sum     |    Sum     |
|-----------+------------+------------+------------+------------|
|AY   |AM   |            |            |            |            |
|-----+-----|            |            |            |            |
|2018 |2    |       22.00|       24.00|           .|           .|
|     |-----+------------+------------+------------+------------|
|     |3    |       20.00|       24.00|       24.00|           .|
|     |-----+------------+------------+------------+------------|
|     |4    |           .|           .|           .|       84.00|
|-----+-----+------------+------------+------------+------------|
|2019 |3    |       60.00|       24.00|           .|           .|
|     |-----+------------+------------+------------+------------|
|     |4    |      100.00|       22.00|       22.00|           .|
-----------------------------------------------------------------

/*___                                                   _
|___ \ _ __  _ __ ___   ___   _ __ ___ _ __   ___  _ __| |_
  __) | `_ \| `__/ _ \ / __| | `__/ _ \ `_ \ / _ \| `__| __|
 / __/| |_) | | | (_) | (__  | | |  __/ |_) | (_) | |  | |_
|_____| .__/|_|  \___/ \___| |_|  \___| .__/ \___/|_|   \__|
      |_|                             |_|
*/

options  FORMCHAR='|----|+|---+=|-/\<>*' ;
options missing=".";
proc report data=sd1.have
  nowd missing
   box;
cols ay am  per, amt ;
define ay  / group ;
define am  / group;
define per  / across ;
define amt / analysis sum missing;
run;quit;

 ------------------------------------------------------------------
 |                                         PER                    |
 |                              6          7          8          9|
 |       AY         AM        AMT        AMT        AMT        AMT|
 |----------------------------------------------------------------|
 |     2018|         2|        22|        24|         .|         .|
 |         |----------+----------+----------+----------+----------|
 |         |         3|        20|        24|        24|         .|
 |         |----------+----------+----------+----------+----------|
 |         |         4|         .|         .|         .|        84|
 |---------+----------+----------+----------+----------+----------|
 |     2019|         3|        60|        24|         .|         .|
 |         |----------+----------+----------+----------+----------|
 |         |         4|       100|        22|        22|        22|
 ------------------------------------------------------------------

/*____                           __
|___ /   _ __  _ __ ___   ___   / _|_ __ ___  __ _
  |_ \  | `_ \| `__/ _ \ / __| | |_| `__/ _ \/ _` |
 ___) | | |_) | | | (_) | (__  |  _| | |  __/ (_| |
|____/  | .__/|_|  \___/ \___| |_| |_|  \___|\__, |
        |_|                                     |_|
*/

 proc freq data=sd1.have;
 tables ay_am*per/nopercent
   norow nocol nocum;
 weight amt;
 run;quit;

 Table of AY_AM by PER

 AY_AM     PER

 Frequency|       6|       7|       8|       9|  Total
 ---------+--------+--------+--------+--------+
 2018_2   |     22 |     24 |      0 |      0 |     46
 ---------+--------+--------+--------+--------+
 2018_3   |     20 |     24 |     24 |      0 |     68
 ---------+--------+--------+--------+--------+
 2018_4   |      0 |      0 |      0 |     84 |     84
 ---------+--------+--------+--------+--------+
 2019_3   |     60 |     24 |      0 |      0 |     84
 ---------+--------+--------+--------+--------+
 2019_4   |    100 |     22 |     22 |     22 |    166
 ---------+--------+--------+--------+--------+
 Total         202       94       46      106      448


/*         _       _   _
 ___  ___ | |_   _| |_(_) ___  _ __  ___
/ __|/ _ \| | | | | __| |/ _ \| `_ \/ __|
\__ \ (_) | | |_| | |_| | (_) | | | \__ \
|___/\___/|_|\__,_|\__|_|\___/|_| |_|___/
 _                   _
(_)_ __  _ __  _   _| |_
| | `_ \| `_ \| | | | __|
| | | | | |_) | |_| | |_
|_|_| |_| .__/ \__,_|\__|
        |_|
*/

options
validvarname=upcase;
libname sd1 "d:/sd1";
data sd1.have;
input ay am per amt;
 ay_am=catx('.',ay,am);
cards4;
2018 2 6 11
2018 2 6 11
2018 2 7 12
2018 2 7 12
2018 3 8 12
2018 3 8 12
2018 4 9 42
2018 4 9 42
2018 3 6 10
2018 3 6 10
2018 3 7 12
2018 3 7 12
2019 3 6 30
2019 3 6 30
2019 3 7 12
2019 3 7 12
2019 4 6 50
2019 4 6 50
2019 4 7 11
2019 4 7 11
2019 4 8 11
2019 4 8 11
2019 4 9 11
2019 4 9 11
;;;;
run;quit;

/*                          _        _           _       _
/ |  _ __  _ __ ___   ___  | |_ __ _| |__  _   _| | __ _| |_ ___
| | | `_ \| `__/ _ \ / __| | __/ _` | `_ \| | | | |/ _` | __/ _ \
| | | |_) | | | (_) | (__  | || (_| | |_) | |_| | | (_| | ||  __/
|_| | .__/|_|  \___/ \___|  \__\__,_|_.__/ \__,_|_|\__,_|\__\___|
    |_|          _     _
 _ __  _ __ ___ | |__ | | ___ _ __ ___
| `_ \| `__/ _ \| `_ \| |/ _ \ `_ ` _ \
| |_) | | | (_) | |_) | |  __/ | | | | |
| .__/|_|  \___/|_.__/|_|\___|_| |_| |_|
|_|
*/

proc tabulate data=sd1.have;
class ay am per;
table ay*am, per*amt*(sum) /rts=13;
var amt;
run;quit;

-----------------------------------------------------------------
|           |                        PER                        |
|           |---------------------------------------------------|
|           |     6      |     7      |     8      |     9      |
|           |------------+------------+------------+------------|
|           |    AMT     |    AMT     |    AMT     |    AMT     |
|           |------------+------------+------------+------------|
|           |    Sum     |    Sum     |    Sum     |    Sum     |
|-----------+------------+------------+------------+------------|
|AY   |AM   |            |            |            |            |
|-----+-----|            |            |            |            |
|2018 |2    |       22.00|       24.00|           .|           .|
|     |-----+------------+------------+------------+------------|
|     |3    |       20.00|       24.00|       24.00|           .|
|     |-----+------------+------------+------------+------------|
|     |4    |           .|           .|           .|       84.00|
|-----+-----+------------+------------+------------+------------|
|2019 |3    |       60.00|       24.00|           .|           .|
|     |-----+------------+------------+------------+------------|
|     |4    |      100.00|       22.00|       22.00|       22.00|
-----------------------------------------------------------------
/*         _       _   _
 ___  ___ | |_   _| |_(_) ___  _ __
/ __|/ _ \| | | | | __| |/ _ \| `_ \
\__ \ (_) | | |_| | |_| | (_) | | | |
|___/\___/|_|\__,_|\__|_|\___/|_| |_|

*/

/*---- Unfortunately you cannot turn off all heading in tabulate like you can do in report ----*/

proc datasets lib=sd1 nolist nodetails;
 delete want prewant;
run;quit;

%utlfkil(d:/tab/frq.txt);

ods _all_ close;
ods listing file="d:/txt/tab.txt";
options  FORMCHAR='|' ;
proc tabulate data=sd1.have;
title1 "|AY|AM|SUM_AMT6|SUM_AMT7|SUM_AMT8|SUM_AMT9";
  class ay am per;
  table ay=''*am=' '
   ,per=''*amt=''*(sum='') /rts=13;
  var amt;
run;quit;
ods listing;

/**************************************************************************************************************************/
/* INTERMEDIATE RESULT                                                                                                    */
/*                                                                                                                        */
/* d:/txt/tab.txt                                                                                                         */
/*                                                                                                                        */
/* |AY|AM|SUM_AMT6|SUM_AMT7|SUM_AMT8|SUM_AMT9                                                                             */
/*                                                                                                                        */
/*                                                                                                                        */
/* |           |     6      |     7      |     8      |     9      |                                                      */
/*                                                                                                                        */
/* |2018 |2    |       22.00|       24.00|           .|           .|                                                      */
/* |                                                                                                                      */
/* |     |3    |       20.00|       24.00|       24.00|           .|                                                      */
/* |                                                                                                                      */
/* |     |4    |           .|           .|           .|       84.00|                                                      */
/*                                                                                                                        */
/* |2019 |3    |       60.00|       24.00|           .|           .|                                                      */
/* |                                                                                                                      */
/* |     |4    |      100.00|       22.00|       22.00|       22.00|                                                      */
/**************************************************************************************************************************/

proc import
 datafile=
  "d:/txt/tab.txt"
 dbms=dlm
 out=prewant
 replace;
 delimiter='|';
 getnames=yes;
 datarow=5;
run;quit;

/**************************************************************************************************************************/
/*WORK.PREWANT total obs=13                                                                                               */
/*   AY     AM    SUM_AMT6    SUM_AMT7    SUM_AMT8    SUM_AMT9    VAR8                                                    */
/*                                                                                                                        */
/*     .     .         .          .           .           .                                                               */
/*     .     6         7          8           9           .                                                               */
/*     .     .         .          .           .           .                                                               */
/*  2018     2        22         24           .           .                                                               */
/*     .     .         .          .           .           .                                                               */
/*     .     3        20         24          24           .                                                               */
/*     .     .         .          .           .           .                                                               */
/*     .     4         .          .           .          84                                                               */
/*     .     .         .          .           .           .                                                               */
/*  2019     3        60         24           .           .                                                               */
/*     .     .         .          .           .           .                                                               */
/*     .     4       100         22          22          22                                                               */
/**************************************************************************************************************************/

data want;
  set prewant(
     drop=var:
     where=(not missing(am)));
run;quit;

/**************************************************************************************************************************/
/* WORK.WANT total obs=5                                                                                                  */
/* Obs     AY     AM    SUM_AMT6    SUM_AMT7    SUM_AMT8    SUM_AMT9                                                      */
/*                                                                                                                        */
/*  1     2018     2        22         24           .           .                                                         */
/*  2        .     3        20         24          24           .                                                         */
/*  3        .     4         .          .           .          84                                                         */
/*  4     2019     3        60         24           .           .                                                         */
/*  5        .     4       100         22          22          22                                                         */
/**************************************************************************************************************************/

/*___                                                     _
|___ \   _ __  _ __ ___   ___   _ __ ___ _ __   ___  _ __| |_
  __) | | `_ \| `__/ _ \ / __| | `__/ _ \ `_ \ / _ \| `__| __|
 / __/  | |_) | | | (_) | (__  | | |  __/ |_) | (_) | |  | |_
|_____| | .__/|_|  \___/ \___| |_|  \___| .__/ \___/|_|   \__|
        |_|      _     _                  |_|
 _ __  _ __ ___ | |__ | | ___ _ __ ___
| `_ \| `__/ _ \| `_ \| |/ _ \ `_ ` _ \
| |_) | | | (_) | |_) | |  __/ | | | | |
| .__/|_|  \___/|_.__/|_|\___|_| |_| |_|
|_|
*/

options  FORMCHAR='|----|+|---+=|-/\<>*' ;
options missing=".";
proc report data=sd1.have
  nowd missing
   box;
cols ay am  per, amt ;
define ay  / group ;
define am  / group;
define per  / across ;
define amt / analysis sum missing;
run;quit;

------------------------------------------------------------------
|                                         PER                    |
|                              6          7          8          9|
|       AY         AM        AMT        AMT        AMT        AMT|
|----------------------------------------------------------------|
|     2018|         2|        22|        24|         .|         .|
|         |----------+----------+----------+----------+----------|
|         |         3|        20|        24|        24|         .|
|         |----------+----------+----------+----------+----------|
|         |         4|         .|         .|         .|        84|
|---------+----------+----------+----------+----------+----------|
|     2019|         3|        60|        24|         .|         .|
|         |----------+----------+----------+----------+----------|
|         |         4|       100|        22|        22|        22|
------------------------------------------------------------------

/*         _       _   _
 ___  ___ | |_   _| |_(_) ___  _ __
/ __|/ _ \| | | | | __| |/ _ \| `_ \
\__ \ (_) | | |_| | |_| | (_) | | | |
|___/\___/|_|\__,_|\__|_|\___/|_| |_|

*/

proc datasets lib=sd1 nolist nodetails;
 delete want prewant;
run;quit;

%utlfkil(d:/txt/rep.txt);

ods _all_ close;
ods listing file="d:/txt/rep.txt";
options  FORMCHAR='|' ;
proc report data=sd1.have
  nowd missing
  noheader formchar="|" box;
title1 "|AY|AM|SUM_AMT6|SUM_AMT7|SUM_AMT8|SUM_AMT9";
cols ay am  per, amt ;
define ay  / group ;
define am  / group;
define per  / across ;
define amt / analysis sum missing;
run;quit;
options  FORMCHAR='|----|+|---+=|-/\<>*' ;
ods listing;

/**************************************************************************************************************************/
/* d:/txt/rep.txt                                                                                                         */
/*                                                                                                                        */
/* |AY|AM|SUM_AMT6|SUM_AMT7|SUM_AMT8|SUM_AMT9|                                                                            */
/*                                                                                                                        */
/*                                                                                                                        */
/*   |     2018|         2|        22|        24|         .|         .|                                                   */
/*   |                                                                                                                    */
/*   |         |         3|        20|        24|        24|         .|                                                   */
/*   |                                                                                                                    */
/*   |         |         4|         .|         .|         .|        84|                                                   */
/*                                                                                                                        */
/*   |     2019|         3|        60|        24|         .|         .|                                                   */
/*   |                                                                                                                    */
/*   |         |         4|       100|        22|        22|        22|                                                   */
/**************************************************************************************************************************/

proc import
 datafile=
  "d:/txt/rep.txt"
 dbms=dlm
 out=prewant
 replace;
 delimiter='|';
 getnames=yes;
 datarow=2;
run;quit;

/**************************************************************************************************************************/
/* WORK. PREWANT total obs=12                                                                                             */
/* VAR1     AY     AM    SUM_AMT6    SUM_AMT7    SUM_AMT8    SUM_AMT9    VAR8                                             */
/*            .     .         .          .           .           .                                                        */
/*            .     .         .          .           .           .                                                        */
/*         2018     2        22         24           .           .                                                        */
/*            .     .         .          .           .           .                                                        */
/*            .     3        20         24          24           .                                                        */
/*            .     .         .          .           .           .                                                        */
/*            .     4         .          .           .          84                                                        */
/*            .     .         .          .           .           .                                                        */
/*         2019     3        60         24           .           .                                                        */
/*            .     .         .          .           .           .                                                        */
/*            .     4       100         22          22          22                                                        */
/*            .     .         .          .           .           .                                                        */
/**************************************************************************************************************************/

data want;
  set prewant(
     drop=var:
     where=(not missing(am)));
run;quit;

/**************************************************************************************************************************/
/* WORK.WANT total obs=5                                                                                                  */
/*                 SUM_     SUM_     SUM_     SUM_                                                                        */
/*   AY     AM     AMT6     AMT7     AMT8     AMT9                                                                        */
/*                                                                                                                        */
/*  2018     2      22       24        .        .                                                                         */
/*     .     3      20       24       24        .                                                                         */
/*     .     4       .        .        .       84                                                                         */
/*  2019     3      60       24        .        .                                                                         */
/*     .     4     100       22       22       22                                                                         */
/**************************************************************************************************************************/

/*____                           __
|___ /   _ __  _ __ ___   ___   / _|_ __ ___  __ _
  |_ \  | `_ \| `__/ _ \ / __| | |_| `__/ _ \/ _` |
 ___) | | |_) | | | (_) | (__  |  _| | |  __/ (_| |
|____/  | .__/|_|  \___/ \___| |_| |_|  \___|\__, |
        |_|      _     _                        |_|
 _ __  _ __ ___ | |__ | | ___ _ __ ___
| `_ \| `__/ _ \| `_ \| |/ _ \ `_ ` _ \
| |_) | | | (_) | |_) | |  __/ | | | | |
| .__/|_|  \___/|_.__/|_|\___|_| |_| |_|
|_|
*/

proc freq data=sd1.have;
tables ay_am*per/nopercent
  norow nocol nocum;
weight amt;
run;quit;

/**************************************************************************************************************************/
/* Table of AY_AM by PER                                                                                                  */
/*                                                                                                                        */
/* AY_AM     PER                                                                                                          */
/*                                                                                                                        */
/* Frequency|       6|       7|       8|       9|  Total                                                                  */
/* ---------+--------+--------+--------+--------+                                                                         */
/* 2018.2   |     22 |     24 |      0 |      0 |     46                                                                  */
/* ---------+--------+--------+--------+--------+                                                                         */
/* 2018.3   |     20 |     24 |     24 |      0 |     68                                                                  */
/* ---------+--------+--------+--------+--------+                                                                         */
/* 2018.4   |      0 |      0 |      0 |     84 |     84                                                                  */
/* ---------+--------+--------+--------+--------+                                                                         */
/* 2019.3   |     60 |     24 |      0 |      0 |     84                                                                  */
/* ---------+--------+--------+--------+--------+                                                                         */
/* 2019.4   |    100 |     22 |     22 |     22 |    166                                                                  */
/* ---------+--------+--------+--------+--------+                                                                         */
/* Total         202       94       46      106      448                                                                  */
/**************************************************************************************************************************/

proc datasets lib=sd1 nolist nodetails;
 delete want prewant;
run;quit;

%utlfkil(d:/txt/frq.txt);

ods _all_ close;
ods listing file="d:/txt/frq.txt";
options  FORMCHAR='|';
ods noproctitle;
proc freq data=sd1.have;
title1 "AY AM|SUM_AMT6|SUM_AMT7|SUM_AMT8|SUM_AMT9|TOTAL";
tables ay_am*per/nopercent
  norow nocol nocum;
weight amt;
run;quit;
options  FORMCHAR='|----|+|---+=|-/\<>*' ;
ods listing;

/**************************************************************************************************************************/
/* |AY|AM|SUM_AMT6|SUM_AMT7|SUM_AMT8|SUM_AMT9                                                                             */
/*                                                                                                                        */
/* Table of AY_AM by PER                                                                                                  */
/*                                                                                                                        */
/* AY_AM     PER                                                                                                          */
/*                                                                                                                        */
/* Frequency|       6|       7|       8|       9|  Total                                                                  */
/*                                                                                                                        */
/* 2018_2   |     22 |     24 |      0 |      0 |     46                                                                  */
/*                                                                                                                        */
/* 2018_3   |     20 |     24 |     24 |      0 |     68                                                                  */
/*                                                                                                                        */
/* 2018_4   |      0 |      0 |      0 |     84 |     84                                                                  */
/*                                                                                                                        */
/* 2019_3   |     60 |     24 |      0 |      0 |     84                                                                  */
/*                                                                                                                        */
/* 2019_4   |    100 |     22 |     22 |     22 |    166                                                                  */
/*                                                                                                                        */
/* Total         202       94       46      106      448                                                                  */
/**************************************************************************************************************************/


proc import
 datafile=
  "d:/txt/frq.txt"
 dbms=dlm
 out=prewant
 replace;
 delimiter='|';
 getnames=yes;
 datarow=4;
run;quit;

data want;
  set prewant( where=(
    not missing(input(ay_am,best.))
    ));
run;quit;

/**************************************************************************************************************************/
/* WORK.WANT total obs=5 23MAY2025:10:26:38                                                                               */
/*  AY_AM     SUM_AMT6    SUM_AMT7    SUM_AMT8    SUM_AMT9    TOTAL                                                       */
/*                                                                                                                        */
/*  2018.2        22         24           0           0        46                                                         */
/*  2018.3        20         24          24           0        68                                                         */
/*  2018.4         0          0           0          84        84                                                         */
/*  2019.3        60         24           0           0        84                                                         */
/*  2019.4       100         22          22          22        166                                                        */
/**************************************************************************************************************************/

/*              _
  ___ _ __   __| |
 / _ \ `_ \ / _` |
|  __/ | | | (_| |
 \___|_| |_|\__,_|

*/
